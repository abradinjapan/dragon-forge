[ list definition ]
!dragon.list(
    allocation !dragon.buffer,
    length !dragon.cell, [ length of content ]
    increase !dragon.cell [ when more space is needed, allocate in increments of 'increase' ]
)

[ open list ]
dragon.list.open(increase !dragon.cell)(list !dragon.list) = {
    [ allocate buffer ]
    dragon.buffer.request(increase)(list:allocation)

    [ setup list data ]
    dragon.copy(dragon.constant.0)(list:length)
    dragon.copy(increase)(list:increase)
}

[ close list ]
dragon.list.close(list !dragon.list)() = {
    [ deallocate buffer ]
    dragon.buffer.return(list:allocation)()
}

[ calculate content buffer ]
dragon.list.calculate.content_buffer(list !dragon.list)(content_buffer !dragon.buffer) = {
    [ calculate content buffer ]
    dragon.copy(list:allocation:start)(content_buffer:start)
    dragon.buffer.calculate.end_address(content_buffer:start, list:length)(content_buffer:end)
}

[ append buffer data ]
dragon.list.append.buffer_data(list !dragon.list, data !dragon.buffer)(list !dragon.list) = {
    [ calculate lengths ]
    dragon.buffer.calculate.length(data)(data.length)
    dragon.buffer.calculate.length(list:allocation)(old_allocation.length)

    [ calculate required length ]
    dragon.integer.add(list:length, data.length)(required_length)

    [ reallocate buffer if necessary ]
    dragon.integer.within_range(dragon.constant.0, required_length, old_allocation.length)(skip, reallocate)
    @reallocate reallocate = {
        [ calculate chunk count ]
        dragon.integer.divide(required_length, list:increase)(increase_chunk_count)
        dragon.integer.add(increase_chunk_count, dragon.constant.1)(increase_chunk_count)

        [ calculate required new allocation size ]
        dragon.integer.multiply(increase_chunk_count, list:increase)(new_allocation.length)

        [ allocate ]
        dragon.buffer.request(new_allocation.length)(new_allocation)

        [ calculate data copy buffers ]
        dragon.list.calculate.content_buffer(list)(copy.source)
        dragon.buffer.calculate.end_address(new_allocation:start, list:length)(destination.end)
        dragon.pack(new_allocation:start, destination.end)(copy.destination !dragon.buffer)

        [ copy old data ]
        dragon.buffer.copy.low_to_high(copy.source, copy.destination)()

        [ delete old allocation ]
        dragon.buffer.return(list:allocation)()

        [ install new allocation ]
        dragon.copy(new_allocation)(list:allocation)
    }

    [ calculate appending addresses ]
    dragon.integer.add(list:allocation:start, list:length)(append_destination.start)
    dragon.buffer.calculate.end_address(append_destination.start, data.length)(append_destination.end)
    dragon.pack(append_destination.start, append_destination.end)(append_destination !dragon.buffer)

    [ append data ]
    dragon.buffer.copy.low_to_high(data, append_destination)()

    [ update length ]
    dragon.copy(required_length)(list:length)
}

[ append cell structure to list ]
dragon.list.append.cell(list !dragon.list, data !dragon.cell)(list !dragon.list) = {
    [ calculate lengths ]
    dragon.structure.byte_size(data)(data.length)
    dragon.buffer.calculate.length(list:allocation)(old_allocation.length)

    [ calculate required length ]
    dragon.integer.add(list:length, data.length)(required_length)

    [ reallocate buffer if necessary ]
    dragon.integer.within_range(dragon.constant.0, required_length, old_allocation.length)(skip, reallocate)
    @reallocate reallocate = {
        [ calculate chunk count ]
        dragon.integer.divide(required_length, list:increase)(increase_chunk_count)
        dragon.integer.add(increase_chunk_count, dragon.constant.1)(increase_chunk_count)

        [ calculate required new allocation size ]
        dragon.integer.multiply(increase_chunk_count, list:increase)(new_allocation.length)

        [ allocate ]
        dragon.buffer.request(new_allocation.length)(new_allocation)

        [ calculate data copy buffers ]
        dragon.list.calculate.content_buffer(list)(copy.source)
        dragon.buffer.calculate.end_address(new_allocation:start, list:length)(destination.end)
        dragon.pack(new_allocation:start, destination.end)(copy.destination !dragon.buffer)

        [ copy old data ]
        dragon.buffer.copy.low_to_high(copy.source, copy.destination)()

        [ delete old allocation ]
        dragon.buffer.return(list:allocation)()

        [ install new allocation ]
        dragon.copy(new_allocation)(list:allocation)
    }

    [ calculate appending addresses ]
    dragon.integer.add(list:allocation:start, list:length)(append_destination.start)
    dragon.buffer.calculate.end_address(append_destination.start, data.length)(append_destination.end)
    dragon.pack(append_destination.start, append_destination.end)(append_destination !dragon.buffer)

    [ append data ]
    dragon.structure_to_buffer(data, append_destination)(advancement)

    [ update length ]
    dragon.copy(required_length)(list:length)
}

[ append new line ]
dragon.list.append.new_line(list !dragon.list)(list !dragon.list) = {
    [ setup string ]
    dragon.set("%0a;")(new_line)

    [ append buffer ]
    dragon.list.append.buffer_data(list, new_line)(list)
}

[ print tabs ]
dragon.list.append.tabs(list !dragon.list, tab_count !dragon.cell)(list !dragon.list) = {
    [ set tab ]
    dragon.set("%09;")(tab)

    [ print tabs until finished ]
    @loop dragon.always = {
        [ check for more characters ]
        dragon.integer.within_range(dragon.constant.0, tab_count, dragon.constant.0)(finished, unfinished)
        dragon.jump.bottom(finished, @loop)()

        [ print tab ]
        dragon.list.append.buffer_data(list, tab)(list)

        [ next index ]
        dragon.integer.subtract(tab_count, dragon.constant.1)(tab_count)

        [ jump to loop start ]
        dragon.jump.top(dragon.always, @loop)()
    }
}

[ append buffer structure to list ]
dragon.list.append.buffer(list !dragon.list, data !dragon.buffer)(list !dragon.list) = {
    [ append members ]
    dragon.list.append.cell(list, data:start)(list)
    dragon.list.append.cell(list, data:end)(list)
}

[ append list structure to list ]
dragon.list.append.list(list !dragon.list, data !dragon.list)(list !dragon.list) = {
    [ append members ]
    dragon.list.append.buffer(list, data:allocation)(list)
    dragon.list.append.cell(list, data:length)(list)
    dragon.list.append.cell(list, data:increase)(list)
}

[ append text location structure to list ]
dragon.list.append.text.location(list !dragon.list, data !dragon.text.location)(list !dragon.list) = {
    [ append members ]
    dragon.list.append.cell(list, data:file_index)(list)
    dragon.list.append.cell(list, data:line_number)(list)
    dragon.list.append.cell(list, data:character_index)(list)
}

[ copy content to new buffer ]
dragon.list.duplicate_content(list !dragon.list)(new_allocation !dragon.buffer) = {
    [ calculate content buffer and size ]
    dragon.list.calculate.content_buffer(list)(content)
    dragon.buffer.calculate.length(content)(content.length)

    [ allocate new buffer ]
    dragon.buffer.request(content.length)(new_allocation)

    [ duplicate data ]
    dragon.buffer.copy.low_to_high(content, new_allocation)()
}
